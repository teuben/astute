#! /bin/csh -f
#
#     use PV (or XYZ) correlation in a selected template to
#     find weaker lines
#
# ccdstat.tab:
#  iz z min  max  N  mean sigma skew kurt sum sum  robust[N mean sig med]
# 1 99916737153.500000  -0.005064 0.004882 79658  -0.000008 0.001247 0.039080 0.046758  -0.660714 0.055385   78977 -0.000008 0.001204 -0.000023
#
#  imstat.tab
#        Frequency  Frequency(Plane) Npts          Sum           Mean          Rms           Std dev       Minimum       Maximum     
#  9.99167409e+10                 0  7.965800e+04 -6.607142e-01 -8.294386e-06  1.246883e-03  1.246864e-03 -5.063728e-03  4.882322e-03



set in=pv1
set auto=1
#  the next 3 won't be used if auto=1
set v0=0
set v1=30
set ref=100.07640
#
set clip=0.01
set clip2=0.01
set vlsr=236
set yapp=/xs
set cmax=0.2
set rms=-1
set mode=1
set blines=$ASTUTE/etc/n253_lines.list
set robust=t
set tab=ccdstat.tab


#           pipeline can override them
pipeset -s csh > tmp$$.par;  source tmp$$.par;  rm tmp$$.par

# poor man's command line parser
foreach arg ($*)
  set $arg
end

set valid_tab=(ccdstat.tab imstat.tab)

echo "@ASTUTE: line_id_nemo"

# ---- below this nothing ought to be changed ----

set c=299792.458

if ($auto) then
  set v01=()
  set ref=0
else
  set v01=(v0=$v0 v1=$v1)
endif

# be nice, if tab= was not produced, find the first existing in $valid_tab
if (! -e $tab) then
  foreach t ($valid_tab)
    if (-e $t) set tab=$t
  end
  if (! -e $tab) then
    echo ERROR: tab=$tab does not exist
    exit 1
  endif
endif


echo =========================================================================
echo Bootstrap Line Identification in `pwd`
if ($tab == $valid_tab[1]) then
  tabmath ccdstat.tab - %2/1e9,%4/%14 all > line_bs.tab
else if ($tab == $valid_tab[2]) then
  tabmath imstat.tab - %1/1e9,%9/%6 all > line_bs.tab
else
  echo tab=$tab not understood, need one of: $valid_tab
  exit 1
endif
tabpeak line_bs.tab mean=t clip=10 | sort -k 3 -nr  > line_bss.tab
if (-z line_bss.tab) then
  echo BAD NEWS: no strong lines to bootstrap from? decrease clip=10?
endif
set f=(`head -1 line_bss.tab | awk '{print $1}'`)
set f0=`nemoinp "$f/(1-$vlsr/$c)" format=%f`
grep -v ^\# $blines | awk 'function abs(x) {return ((x<0.0)?-x:x)}{print abs($1-'$f0'),$1,$2}' | sort -n > lines_bsd.tab
echo -n "LINE_ID: "
head -1 lines_bsd.tab
if ($ref == 0 || $auto == 1) then
  set ref=`head -1 lines_bsd.tab | awk '{print $2}'`
  echo Using ref=$ref
endif
echo =========================================================================


if ($in == pv1 && ! -e $in) then
   fitsccd PV.fits pv1
endif

echo "STATS on $in (robust)"
ccdstat in=$in robust=$robust bad=0
rm -f $in.template
echo PVCORR clip=$clip mode=$mode
echo pvcorr  $in     clip=$clip $v01 vscale=1e-9 rms=$rms mode=$mode out=$in.template 
pvcorr  $in     clip=$clip $v01 vscale=1e-9 rms=$rms mode=$mode out=$in.template > $in.tab
tabpeak $in.tab clip=$clip2 > $in.peak
# take both the peaks and valleys and from the robust sigma maybe get an idea
# what a good value for $clip2 should be
echo =========================================================================
echo TABHIST: $in.tab - check what clip2 might be better to cut out peaks from the noise
tabpeak $in.tab clip=$clip2 valley=t | tabhist - 2 robust=t yapp=/null
echo =========================================================================

set ref0=`sort -k 2 -nr $in.peak | head -1 | awk '{print $1}'`
set ref1=`nemoinp "(1-$vlsr/$c)*$ref" format=%f`
echo REF0=$ref0  REF1=$ref1 REF=$ref
echo "# SkyFreq   RestFreq    Corr"
tabmath $in.peak - "%1-($ref0)+$ref1,%3/(1-$vlsr/$c),%2" all format=%f 
tabmath $in.tab  - "%1-($ref0)+$ref1,%4/(1-$vlsr/$c),%2" all format=%f > $in.tab2
tabplot $in.tab2 2 3 ymin=-$cmax ymax=$cmax point=2,0.1 line=1,1 color=2 ycoord=0,$clip2 headline=$project xlab=RestFreq yapp=$yapp

if (0) then
  # the wrong way, doppler shifts are tricky, especially if the band is going to be wide and/or low freq
  tabmath $in.tab  - "%1-($ref0)+$ref,%4/(1-$vlsr/$c),%2" all format=%f > $in.tab3
  tabplot $in.tab3 1 3 100 102 ymin=-0.2 ymax=0.2 point=2,0.1 line=1,1 color=2 ycoord=0,0.01 headline=$project yapp=$yapp
endif

